/*!
@weakgroup    test
@{
@file
@author         Ming-Hsien Tsai
@data           2023/06/06
@brief          Test variable scope in procedural statements
@version
Portable Test and Stimulus Standard Version 2.0 <br>
> Section 22.7 Procedural constructs <br>

### Test Command

    make test

*/
component pss_top {
  int a = 100;
  int b, c, d, e, f;

  exec init_up {
    e = 0;

    {
      int a = 999;
      b = a;         // b = 999
    }
    c = a;           // c = 100

    if (b == 0)
      int a = 888;
    d = a;           // d = 100

    while(e <= 0) {
      int a = 777;
      e = a;         // e = 777
    }
    f = a;           // f = 100
  }

  action A {
    rand int x;
    exec body ASM =
    """x = {{x}}""";
  }

  action root_a {
    activity {
      do A with { x == this.comp.b; };     // 999
      do A with { x == this.comp.c; };     // 100
      do A with { x == this.comp.d; };     // 100
      do A with { x == this.comp.e; };     // 777
      do A with { x == this.comp.f; };     // 100
    }
  }

};
