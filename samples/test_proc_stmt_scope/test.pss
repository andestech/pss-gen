/*!
@weakgroup    test
@{
@file
@author         Ming-Hsien Tsai
@data           2023/06/06
@brief          Test variable scope in procedural statements
@version
Portable Test and Stimulus Standard Version 2.0 <br>
> Section 22.7 Procedural constructs <br>

### Test Command

    make test

*/
component pss_top {
  int a = 100;
  int b = 1;
  int c = 2;
  int d = 3;
  int e = 4;
  int f = 5;
  int g = 6;
  int h = 7;

  exec init_up {
    {
      int a = 999;
      b = a;         // b = 999
    }
    c = a;           // c = 100

    if (d == d) {
      int a = 888;
      d = a;         // d = 888
    }
    e = a;           // e = 100

    while(f <= e) {
      int a = 777;
      f = a;         // f = 777
    }
    g = a;           // g = 100

    if (g >= a) int h = 666;
  }

  action A {
    rand int x;
    exec body ASM =
    """x = {{x}}""";
  }

  action root_a {
    activity {
      do A with { x == this.comp.b; };     // 999
      do A with { x == this.comp.c; };     // 100
      do A with { x == this.comp.d; };     // 888
      do A with { x == this.comp.e; };     // 100
      do A with { x == this.comp.f; };     // 777
      do A with { x == this.comp.g; };     // 100
      do A with { x == this.comp.h; };     // 7
    }
  }

};
