/*!
@weakgroup      test
@{
@file
@author         Luther Lee
@data           2023/10/27
@brief          Test `foreach` statement.
@version
Portable Test and Stimulus Standard Version 2.1 <br>
> Section 21.7 : Procedural constructs <br>
> Section 21.7.8 : foreach statement <br>


---------------------------------------------------------------------------------------------------

### Test Command
@code{.unparsed}
make gentarget
@endcode
*/
component pss_top {

    enum eSTR2NUM {
        ZERO, ONE, TWO
    };

    struct sSTR2NUM {
        string stringVal = "NULL";
        int intVal = 0;
    };

    action root_a {
        array<int, 3  > intArray = {4  , 5  , 6  };
        list <int     > intList  = {4  , 5  , 6  };
        map  <int, int> intMap   = {4:7, 5:8, 6:9};
        set  <int>      intSet   = {4  , 5  , 6  };

        exec pre_solve {
            foreach (i : intArray[j]) intArray[j] = i + j;
            foreach (i : intList[j] ) intList[j]  = i + j;
            foreach (i : intMap[j]  ) intMap[j]   = i + j;
            foreach (i : intSet     ) intSet.insert(i + intSet.size());
        }

        rand array<int, 3> intArray_rand;
        constraint {
            foreach (i : intArray_rand[j]) intArray_rand[j] in [j+1];
        }

        int intSet_size = -1;
        exec post_solve {
            intSet_size = intSet.size();
        }

        exec body ASM =
"""
// test_foreach
intArray = {{intArray[0]}}, {{intArray[1]}}, {{intArray[2]}}
intList  = {{intList[0]}}, {{intList[1]}}, {{intList[2]}}
intMap   = {{intMap[4]}}, {{intMap[5]}}, {{intMap[6]}}
intSet   = {{intSet_size}}

intArray_rand = {{intArray_rand[0]}}, {{intArray_rand[1]}}, {{intArray_rand[2]}}
""";

        activity {
            foreach (i : intArray[j]) {
                do printf with {msg == "intArray"; index in [j] ; value in [i];};
            }
            foreach (i : intList[j] ) {
                do printf with {msg == "intList" ; index in [j] ; value in [i];};
            }
            foreach (i : intMap[j]  ) {
                do printf with {msg == "intMap"  ; index in [j] ; value in [i];};
            }
            foreach (i : intSet     ) {
                do printf with {msg == "intSet"  ; index in [-1]; value in [i];};
            }
        }
    }

    action printf {
        rand string msg;
        rand int index, value;
        exec body ASM = """{{msg}}, {{index}}, {{value}}""";
    }
}

/**@}*/
